*:/ch [$] = >>>
    include('procs.ms');
    include('jsonprocs.ms');

    if(has_permission('herochat.speak.global')){
        if($){
            @args = parse_args($);
            @args[0] = to_lower(@args[0]);
            @channelfullname = _ncchat_get_channel_full_name(@args[0]);
            @p = player();
            @numargs = array_size(@args);

            # Check if the player entered a channel name for the second argument
            if(!is_null(@channelfullname)){
                if(_ncchat_player_can_speak_in(@p, @channelfullname)){
                    # Set the player's active channel
                    _ncchat_player_set_channel(@p, @channelfullname);
                    msg(colorize('&eNow talking in &f'.@channelfullname.'&e.'));
                } else{
                    msg(colorize('&cYou do not have permission to speak in &f'.@channelfullname.'&c.'));
                }
            } else if(@args[0] == 'qm'){ # the player wants to send a quick message to given channel
                if(@numargs >= 2){
                    # Check if the player has only entered "/ch qm <channel>", and no message
                    if(@numargs == 2){ die(colorize('&cChat message cannot be blank.')); }

                    @channelfullname = _ncchat_get_channel_full_name(to_lower(@args[1]));

                    # Make sure the channel name exists
                    if(!is_null(@channelfullname)){
                        if(_ncchat_player_can_speak_in(@p, @channelfullname)){
                            # Set their channel to what they want the next message to be in
                            @currentchannel = _ncchat_player_get_channel(@p);
                            _ncchat_player_set_channel(@p, @channelfullname);

                            @channelcolor = _ncchat_get_channels()[@channelfullname]['color'];
                            # Send a message in the requested channel
                            chat(array_implode(@args[2..]));

                            # Set their active channel back to what they had before
                            _ncchat_player_set_channel(@p, @currentchannel);
                        } else{
                            msg(colorize('&cYou do not have permission to speak in &f'.@channelfullname.'&c.'));
                        }
                    } else{
                        msg(colorize('&cInvalid channel name or nickname: ').@args[1]);
                    }
                } else{
                    msg(colorize('&cInvalid number of arguments.'));
                    msg(colorize('Usage: /ch qm <channel name/channel nick>'));
                }
            } else if(@args[0] == 'minimal'){
                if(@numargs == 2){
                    # MUST NOT accept player's input directly
                    # player input is string, data is stored as string and not bool
                    # this results in eval to true because non-empty strings are always true
                    # (and me tearing hair out for 5 hours)
                    if(@args[1] == 'true'){
                        @mode = _ncchat_player_set_minimal_mode(@p, true);
                        msg(concat(colorize('&eYou have set minimal mode to &f'), @mode, colorize('&e.')));
                    } else if(@args[1] == 'false'){
                        @mode = _ncchat_player_set_minimal_mode(@p, false);
                        msg(concat(colorize('&eYou have set minimal mode to &f'), @mode, colorize('&e.')));
                    } else{
                        msg(concat(colorize('&eInvalid option &f'), @args[1], colorize('&e (must be either &ftrue&e or &ffalse&e).')));
                    }
                } else{
                    @mode = _ncchat_player_get_minimal_mode(@p);
                    msg(concat(colorize('&eMinimal mode is currently set to &f'), @mode, colorize('&e.')));
                }
            } else{
                msg(colorize('&cInvalid arguments.'));
            }
        } else{
            msg(colorize('&eNCChat Usage:'));
            msg(colorize('/ch <channel name/channel nick> &7- to change channels'));
            msg(colorize('/ch qm <channel name/channel nick> &7- quick message a channel'));
            msg(colorize(concat(
                '/ch minimal <true|false> &7- set minimal mode for yourself',
                'Minimal mode moves each player\'s prefix and suffix into a tooltip over their name.',
            )));
        }
    } else{
        msg(colorize('&cNo permissions.'));
    }
<<<

*:/chtest $ = >>>
    include('procs.ms');
    msg(_ncchat_get_channel_full_name($));
<<<
